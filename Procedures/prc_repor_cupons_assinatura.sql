USE CI_MIDDLEWAY;

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

IF EXISTS (SELECT * FROM DBO.SYSOBJECTS WHERE ID = OBJECT_ID(N'[DBO].[prc_repor_cupons_assinatura]') AND OBJECTPROPERTY(ID, N'ISPROCEDURE') = 1)
DROP PROCEDURE [DBO].[prc_repor_cupons_assinatura]
GO

CREATE PROCEDURE [dbo].[prc_repor_cupons_assinatura]
	@ID_ASSINATURA_CLIENTE INT
 AS

DECLARE @QT_CUPONS_GERAR INT,
		@QT_CUPONS_ATUAIS INT,
		@ATIVO BIT,
		@ID_CLIENTE INT,
		@ID_ASSINATURA INT;

SELECT @QT_CUPONS_GERAR = ISNULL(QT_BILHETE, 0), @ATIVO = AC.IN_ATIVO, @ID_CLIENTE = AC.ID_CLIENTE, @ID_ASSINATURA = AC.ID_ASSINATURA
FROM MW_ASSINATURA A
INNER JOIN MW_ASSINATURA_CLIENTE AC ON AC.ID_ASSINATURA = A.ID_ASSINATURA
WHERE AC.ID_ASSINATURA_CLIENTE = @ID_ASSINATURA_CLIENTE;

SELECT @QT_CUPONS_ATUAIS = COUNT(1)
FROM MW_PROMOCAO
WHERE ID_ASSINATURA_CLIENTE = @ID_ASSINATURA_CLIENTE AND ID_PEDIDO_VENDA IS NULL AND ID_SESSION IS NULL

IF @ATIVO = 0
BEGIN
	DELETE MW_PROMOCAO WHERE ID_ASSINATURA_CLIENTE = @ID_ASSINATURA_CLIENTE AND ID_PEDIDO_VENDA IS NULL AND ID_SESSION IS NULL
END
ELSE
BEGIN
	INSERT INTO MW_PROMOCAO (CD_PROMOCIONAL, CD_CPF_PROMOCIONAL, ID_PROMOCAO_CONTROLE, ID_ASSINATURA_CLIENTE)
	SELECT
		(SELECT CD_CPF FROM MW_CLIENTE WHERE ID_CLIENTE = @ID_CLIENTE),
		(SELECT CD_CPF FROM MW_CLIENTE WHERE ID_CLIENTE = @ID_CLIENTE),
		AP.ID_PROMOCAO_CONTROLE,
		@ID_ASSINATURA_CLIENTE
	FROM MW_ASSINATURA_PROMOCAO AP
	INNER JOIN MW_PROMOCAO_CONTROLE PC ON PC.ID_PROMOCAO_CONTROLE = AP.ID_PROMOCAO_CONTROLE AND PC.CODTIPPROMOCAO = 8
	--CROSS JOIN APENAS PARA GERAR A QUANTIDADE DE REGISTROS NECESSARIA UTILIZANDO A SYS.COLUMNS, QUE POSSIBILITA ATÉ 1000 REGISTROS APROXIMADAMENTE
	CROSS JOIN (SELECT TOP (@QT_CUPONS_GERAR - @QT_CUPONS_ATUAIS) ROW_NUMBER() OVER(ORDER BY (SELECT 1)) FROM SYS.COLUMNS) T(N)
	WHERE AP.ID_ASSINATURA = @ID_ASSINATURA
END